options {
  STATIC = false;
  LOOKAHEAD = 2;
  FORCE_LA_CHECK = true;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = true;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  KEEP_LINE_COLUMN = false;
  VISITOR = true;
  VISITOR_DATA_TYPE = "Object";
  VISITOR_RETURN_TYPE = "Object";
  MULTI = true;
}

PARSER_BEGIN(FilterParser)
/**
 * [Phillip Watson] ("COMPANY") CONFIDENTIAL Unpublished Copyright Â© 2019-2020 Phillip Watson,
 * All Rights Reserved.
 *
 * NOTICE: All information contained herein is, and remains the property of COMPANY. The
 * intellectual and technical concepts contained herein are proprietary to COMPANY and may be
 * covered by U.K. and Foreign Patents, patents in process, and are protected by trade secret or
 * copyright law. Dissemination of this information or reproduction of this material is strictly
 * forbidden unless prior written permission is obtained from COMPANY. Access to the source code
 * contained herein is hereby forbidden to anyone except current COMPANY employees, managers or
 * contractors who have executed Confidentiality and Non-disclosure agreements explicitly covering
 * such access.
 *
 * The copyright notice above does not evidence any actual or intended publication or disclosure of
 * this source code, which includes information that is confidential and/or proprietary, and is a
 * trade secret, of COMPANY. ANY REPRODUCTION, MODIFICATION, DISTRIBUTION, PUBLIC PERFORMANCE, OR
 * PUBLIC DISPLAY OF OR THROUGH USE OF THIS SOURCE CODE WITHOUT THE EXPRESS WRITTEN CONSENT OF
 * COMPANY IS STRICTLY PROHIBITED, AND IN VIOLATION OF APPLICABLE LAWS AND INTERNATIONAL TREATIES.
 * THE RECEIPT OR POSSESSION OF THIS SOURCE CODE AND/OR RELATED INFORMATION DOES NOT CONVEY OR IMPLY
 * ANY RIGHTS TO REPRODUCE, DISCLOSE OR DISTRIBUTE ITS CONTENTS, OR TO MANUFACTURE, USE, OR SELL
 * ANYTHING THAT IT MAY DESCRIBE, IN WHOLE OR IN PART.
 */
package com.hillayes.query.filter.parser;

import java.io.StringReader;

import com.hillayes.query.filter.FilterFunction;
import com.hillayes.query.filter.exceptions.InvalidComparisonException;
import com.hillayes.query.filter.Operator;
import com.hillayes.query.filter.Predicate;
import com.hillayes.query.filter.QueryContext;

/**
 * The starting point for parsing a query filter. This should not be instantiated
 * but, rather, the static method {@link #parse(QueryContext, String)} should be
 * used to perform the parsing.
 *
 * @author <a href="mailto:watson.phill@gmail.com">Phill Watson</a>
 * @since 1.0.0
 */
public class FilterParser
{
    /**
     * This is the preferred method of parsing a query filter. It will parse the
     * given filter expression and, using the given QueryContext, create a node
     * tree that represents the elements of that filter expression.
     * <p>
     * The hierarchy of the nodes in the generated node tree represents the order
     * of precedence of the expressions within the filter; with the lower order
     * nodes appearing first. For example; the operator OR has a lower order then
     * the AND operator so, in the expression "a eq 2 and b gt 3 or c lt 4" the
     * tree hierachy would be:
     * <pre>
     *  Or
     *    And
     *      Comparison (a eq 2) 
     *      Comparison (b gt 3)
     *    Comparison (c lt 4)
     * </pre>
     * When the nodes are evaluated from the bottom-up the AND comparisons will
     * be evaluated before first, with the result of that comparison feeding in
     * to the OR comparisons. 
     * <p>
     * See the JUnit code for more example of how expression are parsed.
     * <p>
     * These nodes can, and should, be traversed using an instance of the interface
     * {@link FilterParserVisitor}. Perhaps the most important method of this
     * interface is {@link FilterParserVisitor#visit(ASTComparison, Object)}.
     * The given ASTComparison object holds an instance of Predicate
     * that fully describes the comparison; the named property, function (if
     * any), the operator (if any) and the value (if any). From this the
     * implementation should be able to construct the equivalent comparison in
     * whatever target notation it requires (e.g. SQL or JPA).
     * 
     * @param aContext the QueryContext implementation that will be used to
     * validate the generated Predicate.
     * @param aFilter the REST URL query filter expression to be parsed.
     * @return the root node of the parsed filter expression.
     *
     * See http://confluence.tibco.com/display/BPM/Building+the+dt-query-filter+Module
     * for more information.
     */
    public static Node parse(QueryContext aContext, String aFilter) throws ParseException, TokenMgrError, InvalidComparisonException
    {
        if ((aFilter != null) && (aFilter.length() > 0))
        {
            FilterParser parser = new FilterParser(new StringReader(aFilter));
            return parser.parse(aContext);
        }

        return(null);
    }
}
PARSER_END(FilterParser)

/**  And now, the actual parsing specifications...... */

/** Skip these characters, they are considered "white space" **/
SKIP : { " " | "\t" | "\r" | "\n" | "\f" }

TOKEN[IGNORE_CASE]: /* RESERVED TOKENS - case-insensitive */
{
      <AND: "and" >
    | <OR:  "or" >
    | <NOT: "not" >
    | <EQ: "eq" >
    | <NE: "ne" >
    | <GT: "gt" >
    | <LT: "lt" >
    | <GE: "ge" >
    | <LE: "le" >
    | <LPAREN: "(" >
    | <RPAREN: ")" >
    | <COMMA: "," >
}

TOKEN: /* RESERVED TOKENS - case-sensitive */
{
      <TOUPPER: "upper" >
    | <TOLOWER: "lower" >
    | <CONTAINS: "contains" >
    | <ENDSWITH: "endswith" >
    | <STARTSWITH: "startswith" >
    | <NOTNULL: "notnull" >
    | <ISNULL: "isnull" >
}

TOKEN: /* LITERALS */
{
      <#DIGIT: ["0" - "9"] >
    | <#ALPHA: ["A"-"Z" , "a"-"z", "_", "$"] >
    | <#INTEGER: ( <DIGIT> )+ >
    | <DOT: "." >
    | <NUMERIC: (["+","-"])? ( <INTEGER> | ( <INTEGER> <DOT> <INTEGER> ) ) >
    | <QUOTED_TEXT: ( "\"" ( ~["\""] )* "\"" ) | ( "'" ( ~["'"] )* "'" ) >
    | <IDENTIFIER: <ALPHA> (<ALPHA> | <DIGIT> | <DOT> | "-")* > 
}

/**
 * The start of the parsing process. This will simply apply the "orExpression"
 * lexical analysis. The order in which the subsequent methods are called will
 * determine the order of precedence given to the AND, OR and NOT operators,
 * and how brackets can be used to override that precedence order.
 */
Node parse(QueryContext aContext) throws InvalidComparisonException : { }
{
    orExpression(aContext) <EOF>
    { return jjtThis; }
}

/**
 * Looks for a sequence of "andExpression" nodes and, if more than one, will
 * combine them under an "or" node.
 */
void orExpression(QueryContext aContext) throws InvalidComparisonException #void : { }
{
    // return either a single expression node
    // or multple expression nodes enclosed in an OR node 
    (
      andExpression(aContext)
      (
        <OR> { aContext.queryBuilder().append(" OR "); }
        andExpression(aContext)
      )*
    ) #Or(>1)
}

/**
 * Looks for a sequence of "expression" nodes and, if more than one, will
 * combine them under an "and" node.
 */
void andExpression(QueryContext aContext) throws InvalidComparisonException #void : { }
{
    // return either a single expression node
    // or multple expression nodes enclosed in an AND node 
    (
      expression(aContext)
      (
        <AND> { aContext.queryBuilder().append(" AND "); }
        expression(aContext)
      )*
    ) #And(>1)
}

/**
 * Looks for a comparison expression or a NOT comparison expression or a
 * sequence of expression contained within brackets.
 */
void expression(QueryContext aContext) throws InvalidComparisonException #void : { }
{
    // return either a comparison node
    // or a comparison node enclosed in a NOT node
    // or the start of a new expression enclosed in brackets
    Comparison(aContext)

    | (
        <NOT> { aContext.queryBuilder().append("NOT "); }
        orExpression(aContext) #Not
      )

    | (
        <LPAREN> { aContext.queryBuilder().append('('); }
        orExpression(aContext)
        <RPAREN> { aContext.queryBuilder().append(')'); }
      )
}

/**
 * The start of a comparison. This will call the QueryContext to create a new
 * Predicate, and then populate that Predicate before asking
 * the QueryComparison to validate it. If the QueryComparison says it's invalid,
 * a InvalidComparisonException will be raised.
 */
void Comparison(QueryContext aContext) throws InvalidComparisonException: { Predicate predicate = aContext.newPredicate(); }
{
    // look for a null function, boolean function or a comparison expression
    ( NullFunction(aContext, predicate) | BooleanFunction(aContext, predicate) | PredicateExpr(aContext, predicate) )

    {
      // validate the resulting expression
      if (! predicate.isValid())
      {
         throw new InvalidComparisonException(predicate);
      }

      // record the Predicate in the resulting ASTComparison node
      jjtThis.jjtSetValue(predicate);
      
      predicate.appendTo(aContext.queryBuilder());
    }
}

/**
 * Looks for one of the possible NULL expressions and populates the given
 * Predicate with the name of the function, the name of the property
 * to be passed to the function. The format is expected to be:
 *
 *    function(property)
 */
void NullFunction(QueryContext aContext, Predicate aPredicate) #void : { }
{
    (
      // look for one of the known functions and record its name
        <ISNULL>  { aPredicate.setFunction(FilterFunction.ISNULL); }
      | <NOTNULL> { aPredicate.setFunction(FilterFunction.NOTNULL); }
    )

    <LPAREN>
    // look for a propety name parameter to the function
    <IDENTIFIER> { aPredicate.setName(getToken(0).image); }
    <RPAREN>
}

/**
 * Looks for one of the possible boolean expressions and populates the given
 * Predicate with the name of the function, the name of the property
 * and the value to be passed to the function. The format is expected to be:
 *
 *    function(property, value)
 */
void BooleanFunction(QueryContext aContext, Predicate aPredicate) #void : { }
{
    (
      // look for one of the known functions and record its name
        <CONTAINS>   { aPredicate.setFunction(FilterFunction.CONTAINS); }
      | <ENDSWITH>   { aPredicate.setFunction(FilterFunction.ENDSWITH); }
      | <STARTSWITH> { aPredicate.setFunction(FilterFunction.STARTSWITH); }
    )

    <LPAREN>
    // look for a propety name parameter to the function
    <IDENTIFIER> { aPredicate.setName(getToken(0).image); }
    <COMMA>
    // look for the value parameter to the function
    <QUOTED_TEXT> { aPredicate.setString(getToken(0).image); }
    <RPAREN>
}

/**
 * Looks for a comparison, with a possible function applied to the named
 * property. It will populate the given Predicate with attributes
 * of the comparison (function name, property name, operator and value).
 * The format is expected to be:
 *
 *    function(property) operator value
 * or
 *    property operator value
 */
void PredicateExpr(QueryContext aContext, Predicate aPredicate) #void : { }
{
    (
      // look for either a function OR a property name and record the info
        UnaryFunction(aContext, aPredicate)
      | <IDENTIFIER> { aPredicate.setName(getToken(0).image); }
    )

    (
      // record the operator to be applied
        <EQ> { aPredicate.setOperator(Operator.EQ); }
      | <NE> { aPredicate.setOperator(Operator.NE); }
      | <GT> { aPredicate.setOperator(Operator.GT); }
      | <LT> { aPredicate.setOperator(Operator.LT); }
      | <GE> { aPredicate.setOperator(Operator.GE); }
      | <LE> { aPredicate.setOperator(Operator.LE); }
    )

    (
      // look for either an enum constant, a quoted string or a number
        <IDENTIFIER>  { aPredicate.setIdentifier(getToken(0).image); }
      | <QUOTED_TEXT> { aPredicate.setString(getToken(0).image); }
      | <NUMERIC>     { aPredicate.setNumber(getToken(0).image); }
    )
}

/**
 * Looks for one of the possible functions that take a single named property
 * as a parameter, and populates the given Predicate with the name of
 * that function and the name of the property. The format is expected to be:
 *
 *    function(property)
 */
void UnaryFunction(QueryContext aContext, Predicate aPredicate) #void : { }
{
    (
        <TOLOWER> { aPredicate.setFunction(FilterFunction.LOWER); }
      | <TOUPPER> { aPredicate.setFunction(FilterFunction.UPPER); }
    )
    
    <LPAREN>
    <IDENTIFIER> { aPredicate.setName(getToken(0).image); }
    <RPAREN>
}
